#!groovy
// The above triggers groovy syntax highlighting in vim

apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "idea"
apply plugin: "eclipse"

//archiveBaseName = "properties-plugin"
version = "1.2.0"
group = "net.saliman"
archivesBaseName = "gradle-data-extractor-plugin"
def mavenRepoUploadUrl = "scp://maxdevcloud/var/www/maven/repo"
def mavenRepoUploadUser = "git"

repositories {
	mavenCentral() 
}

configurations {
	archives
}

dependencies {
	compile localGroovy()
	compile gradleApi()
	archives "org.apache.maven.wagon:wagon-ssh:2.2"
	archives "org.apache.maven.wagon:wagon-ssh-external:2.2"
}

task sourceJar(type: Jar) {
	description = "An archive of the source code for Maven Central"
	classifier = "sources"
	from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
	description = "An archive of the GroovyDocs for Maven Central"
	classifier = 'javadoc'
	from groovydoc
}

artifacts {
    archives jar, groovydocJar, sourceJar
}

uploadArchives {
	doFirst {
		repositories {
			mavenDeployer {
				configuration = configurations.archives
				repository(url: mavenRepoUploadUrl) {
					authentication(userName: mavenRepoUploadUser,
								   privateKey: file("${privateKeyFile}"),
								   passphrase: "${privateKeyPassphrase}")
				}
			}
		}
	}
}

idea {
	module {

		//if you prefer different output folders
		inheritOutputDirs = false
		outputDir = file('build/idea/out')
		testOutputDir = file('build/idea/testOut')
		//if you love browsing Javadoc
		downloadJavadoc = true
		//and hate reading sources :)
		downloadSources = false
	}
}

// Configure Idea plugin so that it generates project files that use git for 
// source control.  Thank you to Eric Wendelin for showing me this trick.
idea.project.ipr.withXml { provider ->
	def node = provider.asNode()
	def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
	vcsConfig.mapping[0].'@vcs' = 'Git'

	// set gradle home
	def gradleSettings = node.appendNode('component',[name: 'GradleSettings'])
	gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir.absolutePath])
}

// Get the keyfile, user, and passphrase for uploading, if they aren't already
// defined.
gradle.taskGraph.whenReady { taskGraph ->
	if ( taskGraph.allTasks.find { it.name == "uploadArchives" } != null ) {
		Console console = System.console()

		if ( !project.hasProperty("privateKeyFile") ) {
			if ( console == null ) {
				throw new MissingPropertyException("No privateKeyFile given")
			}
		    def keyfile = console.readLine("\nPrivate Key file: ")
			project.ext.privateKeyFile = keyfile
		}

		if ( !project.hasProperty("privateKeyPassphrase") ) {
			if ( console == null ) {
				throw new MissingPropertyException("No privateKeyPassphrase given")
			}
		    def passphrase = console.readPassword("\nPassphrase for ${privateKeyFile}: ")
			project.ext.privateKeyPassphrase = passphrase
		}
	}
}

def getPomConfiguration() {
	return {
		name 'Gradle Data Extractor Plugin'
		packaging 'jar'
		description 'A Gradle plugin for extracting test data from a database to a SQL file.'
		url 'https://github.com/stevesaliman/gradle-data-extractor-plugin'
		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}
		developers {
			developer {
				id 'stevesaliman'
				name 'Steven C. Saliman'
				email 'support@saliman.net'
			}
		}
		scm {
			connection 'scm:https://stevesaliman@github.com/stevesaliman/gradle-data-extractor-plugin'
			developerConnection 'scm:git@github.com:stevesaliman/gradle-data-extractor-plugin.git'
			url 'https://github.com/stevesaliman/gradle-data-extractor-plugin'
		}
	}
}
